# Maintainer: DuckSoft <realducksoft@gmail.com>
_realname=across
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.2
pkgrel=1
pkgdesc="The next GUI client for v2ray core"
arch=("any")
mingw_arch=("mingw32" "mingw64" "ucrt64" "clang64" "clang32")
url="https://github.com/ArkToria/ACross"
license=("GPL3")
depends=("${MINGW_PACKAGE_PREFIX}-qt6-base" "${MINGW_PACKAGE_PREFIX}-qt6-svg" "${MINGW_PACKAGE_PREFIX}-qt6-translations"
  "${MINGW_PACKAGE_PREFIX}-qt6-tools" "${MINGW_PACKAGE_PREFIX}-qt6-imageformats" "${MINGW_PACKAGE_PREFIX}-qt6-5compat"
  "${MINGW_PACKAGE_PREFIX}-curl" "${MINGW_PACKAGE_PREFIX}-fmt" "${MINGW_PACKAGE_PREFIX}-grpc"
  "${MINGW_PACKAGE_PREFIX}-nlohmann-json" "${MINGW_PACKAGE_PREFIX}-protobuf" "${MINGW_PACKAGE_PREFIX}-spdlog"
  "${MINGW_PACKAGE_PREFIX}-zxing-cpp"
)
makedepends=("git" "${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-clang" "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-gtest")
optdepends=("v2ray: use system v2ray core."
  "noto-fonts: default display fonts")

source=("${_realname}-${pkgver}.tar.gz::https://github.com/ArkToria/ACross/archive/refs/tags/v${pkgver}.tar.gz")

sha256sums=("6e5c15bbc55e38204852011096f2afc09c35f1c1682f45da3cc85a005a5eaa3e")

build() {
  # Shared Build
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"

  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake.exe \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=OFF \
    -DENABLE_DEPLOYMENT=OFF \
    -DBUILD_INFO="Mingw64 - ${pkgver}" \
    "../${_realname}-${pkgver}"

  cmake --build .
}

package() {
  # Shared Install
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR="${pkgdir}" cmake --install .
}
