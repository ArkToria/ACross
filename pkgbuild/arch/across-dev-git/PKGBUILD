# Maintainer: DuckSoft <realducksoft@gmail.com>
pkgname=across-dev-git
_pkgname=across
pkgver=0.1.0.r177.a8e9465
pkgrel=1
pkgdesc="The next GUI client for v2ray core (Development Branch)"
arch=('x86_64')
url='https://github.com/ArkToria/ACross'
license=('GPL3')
depends=('hicolor-icon-theme' 'qt6-base' 'qt6-svg' 'qt6-quickcontrols2' 'qt6-translations'
    'qt6-tools' 'qt6-imageformats' 'qt6-5compat' 'curl' 'fmt' 'grpc'
    'jsoncpp' 'libsodium' 'protobuf' 'spdlog' 'sqlite' 'zxing-cpp')

optdepends=('v2ray: use system v2ray core.'
    'noto-fonts: default display fonts')

makedepends=('git' 'cmake' 'ninja' 'gcc' 'gtest')

source=('across-dev-git::git+https://github.com/ArkToria/ACross.git#branch=qt6_dev')

sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}/"
    printf "%s.r%s.%s" $(cat ./misc/VERSION) $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

prepare() {
    cd "${srcdir}/${pkgname}/"
    git submodule update --init --recursive
}

build() {
    cd "${srcdir}/${pkgname}/"

    mkdir -p build && cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=${pkgdir}/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -GNinja
    ninja
}

package() {
    cd "${srcdir}/${pkgname}"
    ninja -C "build" install
}

